add_executable(wclang wclang.cpp wclang_time.cpp wclang_cache.cpp)
install(TARGETS wclang DESTINATION bin)

option(SYMLINK_ALL_TRIPLETS "symlink all triplets" OFF)
set(SYMLINK_TRIPLETS ${VALID_TRIPLETS})
if(SYMLINK_ALL_TRIPLETS)
  set(SYMLINK_TRIPLETS ${TRIPLETS})
endif ()

foreach (TRIPLET ${SYMLINK_TRIPLETS})
  install(CODE "set(FINAL_DIR ${CMAKE_INSTALL_PREFIX})
                if(NOT \"\$ENV{DESTDIR}\" STREQUAL \"\")
                  set(FINAL_DIR \$ENV{DESTDIR}${CMAKE_INSTALL_PREFIX})
                endif()
                message(STATUS \"Symlinking: \${FINAL_DIR}/bin/${TRIPLET}-clang -> wclang\")
                execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink wclang ${TRIPLET}-clang WORKING_DIRECTORY \${FINAL_DIR}/bin)
                message(STATUS \"Symlinking: \${FINAL_DIR}/bin/${TRIPLET}-clang++ -> wclang\")
                execute_process(COMMAND ${CMAKE_COMMAND} -E create_symlink wclang ${TRIPLET}-clang++ WORKING_DIRECTORY \${FINAL_DIR}/bin)")
endforeach()
